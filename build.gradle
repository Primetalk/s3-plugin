import com.amazonaws.auth.DefaultAWSCredentialsProviderChain

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath group: 'com.amazonaws', name: 'aws-java-sdk-core', version: '1.11.+'
    }
}

plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'idea'
    id 'groovy'
}

wrapper {
    gradleVersion = '6.0.1'
}

group = 'com.mgd.core'
version = '1.0.1'

jar {
    manifest {
        attributes([
                'Manifest-Version': '1.0',
                'Build-Tag'       : "$buildTag"
        ])
    }
}

repositories {
    mavenCentral()
}

ext {
    amazonAWSVersion = '1.11.690'
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    api group: 'com.amazonaws', name: 'aws-java-sdk-s3', version: amazonAWSVersion
}

gradlePlugin {
    plugins {
        s3Plugin {
            id = 'com.mgd.core.gradle.s3'
            implementationClass = 'com.mgd.core.gradle.S3Plugin'
        }
    }
}

publishing {
    repositories {
        maven {
            name 'MGD-Maven'
            url 's3://mgd-maven'
            credentials(AwsCredentials) {
                def defaultCredentials = new DefaultAWSCredentialsProviderChain().getCredentials()
                accessKey defaultCredentials.getAWSAccessKeyId()
                secretKey defaultCredentials.getAWSSecretKey()
            }
        }
    }

    publications {
        pluginMaven(MavenPublication) {
            pom.withXml {
                asNode().appendNode('properties').appendNode('build.tag', buildTag)
            }
        }

    }
}
